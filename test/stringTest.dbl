;*****************************************************************************
;
; Title:        stringTest
;
; Description:  A test routine for the StringUtil class
;
;*****************************************************************************
;
; This code is supplied as seen and without warranty or support, and is used
; at your own risk. Neither the author or Synergex accept any responsability
; for any loss or damage which may result from the use of this code.
;
;*****************************************************************************

import SynPSG.System

subroutine stringTest

    record
        tt          ,i4         ;Terminal channel
        formatString,string
        result      ,string
        group dt    ,a20
            group myDate    ,a
                year        ,a4
                mon         ,a2
                day         ,a2
            endgroup
            group myTime    ,a
                hh          ,a2
                mm          ,a2
                ss          ,a2
            endgroup
            uuuuuu          ,a6
        endgroup
        dayname         ,[7]a3  ,"Sun", "Mon", "Tue", "Wed", "Thu", "Fri", "Sat"
        monthname       ,[12]a3 ,"Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec"

        s1          ,string
        s2          ,string
        s3          ,string
        sArray      ,[#]string

proc

    open(tt=%syn_freechn,i,'tt:')

    xcall flags(7004020,1)

    writes(tt,"")
    writes(tt,"StringText - A test program for String.Format()")
    writes(tt,"")

    dt = %datetime
    formatString = "{0:ddd dd MMM yyyy HH:mm:ss zzz}"
    result = SynPSG.StringUtil.Format(formatString, dt)

    writes(tt, "datetime ="+dt)
    writes(tt, "format   ="+formatString)
    writes(tt, "result   ="+result)
    writes(tt, "expecting "+dayname[%wkday]+" "+day+" "+monthname[^d(mon)]+" "+year+" "+hh+":"+mm+":"+ss+" -08:00")

    writes(tt, "")
    writes(tt, "Datetime format ")
    writes(tt, "s "+SynPSG.StringUtil.Format("{0:s}", %datetime))
    writes(tt, "u "+SynPSG.StringUtil.Format("{0:u}", %datetime))
    writes(tt, "D "+SynPSG.StringUtil.Format("{0:D}", %datetime))
    writes(tt, "g "+SynPSG.StringUtil.Format("{0:g}", %datetime))
    writes(tt, "")

    s1 = 'a|b|c'
    sArray = SynPSG.StringUtil.Split(s1, '|')

    writes(tt, "")
    display(tt, "Split '"+s1+"' into")
    foreach s3 in sArray
        display(tt, " '"+s3+"'")
    writes(tt, "")

    s2 = SynPSG.StringUtil.Replace(s1, 'b', 'B')

    writes(tt, "")
    writes(tt, "Replace 'b' with 'B' in "+s1+" gives "+s2)
    writes(tt, "")

    xreturn
end
